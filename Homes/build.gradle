plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'codes.shiftmc'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
    compileOnly "dev.jorel:commandapi-bukkit-core:9.5.0"
    implementation 'org.projectlombok:lombok:1.18.34'
    implementation 'com.zaxxer:HikariCP:5.1.0'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    // Removed classifier and version from the jar name
    archiveClassifier.set('') // Remove the classifier to replace the default JAR
    archiveVersion.set('') // Optional: remove version from the jar name if desired
}

task moveShadowedJar(type: Copy, dependsOn: shadowJar) {
    from shadowJar.archiveFile.get().asFile.parentFile
    include shadowJar.archiveFile.get().asFile.name
    into '../run/plugins/'

    doLast {
        println "Successfully moved ${shadowJar.archiveFile.get().asFile.name} to ../../run/plugins/"
    }
}

build.dependsOn moveShadowedJar